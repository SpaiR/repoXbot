plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '4.0.2'
    id 'org.jetbrains.kotlin.jvm' version '1.3.0'
    id 'com.gradle.build-scan' version '1.16'
    id 'org.jmailen.kotlinter' version '1.20.1'
}

wrapper {
    gradleVersion = '5.1'
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
    publishAlwaysIf(System.getenv('CI') != null)
}

ext {
    kotlinVersion = '1.3.0'
    vertxVersion = '3.5.4'
    jsoupVersion = '1.11.3'
    junitVersion = '5.3.1'
}

repositories {
    jcenter()
    mavenLocal()
}

group = 'io.github.spair.repoxbot'
version = '1.1.1-SNAPSHOT'

sourceCompatibility = '1.8'
mainClassName = 'io.vertx.core.Launcher'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "io.vertx:vertx-core:$vertxVersion"
    implementation "org.jsoup:jsoup:$jsoupVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = sourceCompatibility
    }
}

shadowJar {
    getArchiveFileName().set("${rootProject.name}.jar")

    manifest {
        attributes('Main-Verticle': 'io.github.spair.repoxbot.MainVerticle')
    }

    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

task cleanProject(type: Delete, group: 'build') {
    description = 'Deletes the build directory and project generated files.'
    dependsOn clean
    delete "$projectDir/.vertx", "$projectDir/logs", "$projectDir/out"
}

test { 
    useJUnitPlatform()
}
